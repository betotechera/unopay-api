import java.util.regex.Matcher

buildscript {
	ext {
		springBootVersion = '1.5.1.RELEASE'
	}
	repositories {
		mavenCentral()
	}
	dependencies {
		classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
		classpath 'net.researchgate:gradle-release:2.4.0'
	}

}

plugins {
	id 'net.researchgate.release' version '2.4.0'
}

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'org.springframework.boot'

jar {
	baseName = 'unopay-api'
	version = ''
}

springBoot {
	executable = true
}

sourceCompatibility = 1.8


repositories {


	maven {
		url "${nexusUrl}/content/repositories/releases"
	}
		mavenCentral()
}


release {
	failOnCommitNeeded = true
	failOnPublishNeeded = true
	failOnSnapshotDependencies = true
	failOnUnversionedFiles = true
	failOnUpdateNeeded = true
	revertOnFail = true
	tagCommitMessage = 'creating tag: '
	buildTasks = ['build']
	tagTemplate = 'unopay-api-${version}'
	versionPatterns = [
			/(\d+)([^\d]*$)/: { Matcher m, Project p -> m.replaceAll("${(m[0][1] as int) + 1}${m[0][2]}") }
	]
	scmAdapters = [
			net.researchgate.release.GitAdapter,
	]

	git {
		requireBranch = 'master'
		pushToRemote = 'origin'
		commitVersionFileOnly = false
	}
}

bootRun {
	systemProperties System.properties
}


dependencies {
	compile 'org.springframework.boot:spring-boot-starter-actuator'
	compile 'org.springframework.boot:spring-boot-starter-web'
	compile 'org.springframework.boot:spring-boot-starter-security'
	compile'org.springframework.security.oauth:spring-security-oauth2'
	compile 'org.springframework.boot:spring-boot-starter-data-jpa'
	compile group: 'com.google.guava', name: 'guava', version: '11.0.2'
	compile group: 'org.springframework.boot', name: 'spring-boot-starter-redis', version: '1.0.0.RELEASE'

	compile 'org.hsqldb:hsqldb'
	compileOnly 'org.projectlombok:lombok:1.16.14'
	testCompile 'org.springframework.boot:spring-boot-starter-test'
}
